/*
 * PSS (private Schulspezifikation) was bedeutet:
 *
 * | steht für eine Alternative,
 * [ ] steht für Optionen (kann, muss aber nicht)
 * { } steht für Wiederholungen (auch 0-mal)
 * ( ) erzeugen logische Gruppierungen
 * Am Ende einer Anweisung steht ein "Ende"-Zeichen (z.B. ein Strichpunkt oder Punkt)
 * Statt der Ableitungspfeile werden "=" verwendet.
 * Das leere Element wird mit Epsilon bezeichnet
 *
 * Sonst gibt es keine weiteren Konstruktionselemente.
 */

PSS-Grammar   ::= Rule+ EOF
Rule          ::= Nonterminal ( '=' | ':' | '::=' )  Alternatives ';'?
Alternatives  ::= Alternative ( '|' Alternative )*
Alternative   ::= 'ε'? | Item+
Item          ::= ( Nonterminal
                  | Terminal
                  | '[' Alternatives ']'
                  | '{' Alternatives '}'
                  | '(' Alternatives ')'
                  )
                  ( '*' | '+' | '?' )?

<?TOKENS?>

Nonterminal   ::= ( NameStartChar NameChar* ) - 'ε'
NameStartChar ::= [A-Z] | '_' | [a-z]
                | [#xC0-#xD6] | [#xD8-#xF6] | [#xF8-#x2FF]
                | [#x370-#x37D] | [#x37F-#x1FFF] | [#x200C-#x200D]
                | [#x2070-#x218F] | [#x2C00-#x2FEF] | [#x3001-#xD7FF]
                | [#xF900-#xFDCF] | [#xFDF0-#xFFFD] | [#x10000-#xEFFFF]
NameChar      ::= NameStartChar | '-' | '.' | [0-9] | #xB7
                | [#x0300-#x036F] | [#x203F-#x2040]
Terminal      ::= '"' [^"#xA#xD]* '"'
                | "'" [^'#xA#xD]* "'"
Whitespace    ::= [#x9#xD#xA#x20]+
                | '#' [^#xA]*
                  /*ws:definition*/
EOF           ::= $

/*
 * oder in PSS:
 *
 *    PSS-Grammar   = Rule { Rule }
 *                  ;
 *    Rule          = Nonterminal '=' Alternatives ';'
 *                  ;
 *    Alternatives  = Alternative { '|' Alternative }
 *                  ;
 *    Alternative   = Item { Item }
 *                  ;
 *    Item          = 'ε'
 *                  | Nonterminal
 *                  | Terminal
 *                  | '[' Alternatives ']'
 *                  | '{' Alternatives '}'
 *                  | '(' Alternatives ')'
 *                  ;
 *    Nonterminal   = ???
 *    NameStartChar = ???
 *    NameChar      = ???
 *    Terminal      = ???
 *    Whitespace    = ???
 */